apply plugin: 'com.android.application'

ext.vMajor = 1
ext.vMinor = 0
ext.vPatch = 0
ext.minSdkVersion = 26

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "fr.nhqml.iwillgetajob"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion 28
        versionCode genVersionCode()
        versionName genVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        def appName = "Iwillgetajob"
        outputFileName = appName+"-${variant.versionName}.apk"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'

    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'
}

private Integer genVersionCode() {
    return ext.minSdkVersion * 10000000 + ext.vMajor * 10000 + ext.vMinor * 100 + ext.vPatch
}

private String genVersionName() {
    return "${ext.vMajor}.${ext.vMinor}.${ext.vPatch}"
}